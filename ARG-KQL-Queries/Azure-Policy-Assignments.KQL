// Type: ARG KQL query
// Purpose: To find information on current policies assigned on a specific subscription, especially for a specific category
// Use at your own discretion. No guarantees provided
policyresources
| where type in ("microsoft.authorization/policyassignments")
//| where ['kind'] == "policyassignments"
| where subscriptionId == "<<subscription_id>>"
| extend PropertiesJSON = parse_json(properties)
| extend DisplayName = PropertiesJSON.displayName, Effect = PropertiesJSON.parameters.effect.value, 
CreatedBy = PropertiesJSON.metadata.createdBy, CreatedOn = PropertiesJSON.metadata.createdOn, 
UpdatedBy = PropertiesJSON.metadata.updatedBy, UpdatedOn = PropertiesJSON.metadata.updatedOn, 
AssignedBy = PropertiesJSON.metadata.assignedBy, PolicyDefinitionId = tostring(PropertiesJSON.policyDefinitionId)
| extend DefinitionType = case(PolicyDefinitionId contains_cs  "policySetDefinitions", "Initiative", "Policy")
| project-away PropertiesJSON
| join kind=inner (policyresources
| where type in ("microsoft.authorization/policydefinitions", "microsoft.authorization/policysetdefinitions")
//| where ['kind'] in ("policyDefinitions", "policySetDefinitions")
| extend PropertiesJSON = parse_json(properties)
| extend Category = tostring(PropertiesJSON.metadata.category)
| extend PolicyDisplayName = PropertiesJSON.displayName
| project ['id'], Category, PolicyDisplayName) on $left.PolicyDefinitionId == $right.['id']
| project DisplayName, PolicyID = ['id1'], name, PolicyDisplayName, ['kind'], Effect, DefinitionType, Category, resourceGroup, 
subscriptionId, managedBy, plan, identity, AssignedBy, CreatedOn, CreatedBy, UpdatedOn, UpdatedBy
//| where Category in ('Kubernetes', 'Container Registry', 'Security Center') //use this filter for further filtering on a specific category of policy assignments.
